plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' //um Fat-Jar mit allen Dependendies zu erzeugen
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    // Generate a sources JAR alongside the main JAR
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

// Resolve tools.jar from preferred locations:
// 1) java.home (current runtime, preferred, wie IntelliJ/Gradle-JVM)
// 2) JAVA_HOME
// 3) PATH (beliebiges installiertes Java, aus bin zurück auf Home schließen)
def resolveToolsJar() {
    // 1) Try current runtime (java.home)
    def javaHomeProp = System.properties['java.home']
    if (javaHomeProp != null) {
        def runtimeHome = new File(javaHomeProp)
        def jdkHome = runtimeHome.name.equalsIgnoreCase("jre")
                ? runtimeHome.parentFile
                : runtimeHome
        def toolsFromJavaHome = new File(jdkHome, "lib/tools.jar")
        if (toolsFromJavaHome.exists()) {
            return toolsFromJavaHome.canonicalFile
        }
    }

    // 2) Try JAVA_HOME
    def javaHomeEnv = System.getenv('JAVA_HOME')
    if (javaHomeEnv != null) {
        def envHome = new File(javaHomeEnv)
        def toolsFromEnv = new File(envHome, "lib/tools.jar")
        if (toolsFromEnv.exists()) {
            return toolsFromEnv.canonicalFile
        }
    }

    // 3) Try PATH entries
    def pathEnv = System.getenv('PATH')
    if (pathEnv != null) {
        def toolsFromPath = null

        pathEnv.split(File.pathSeparator).each { pathEntry ->
            if (toolsFromPath != null) {
                return
            }

            def binDir = new File(pathEntry)
            def javaExeName = isWindowsOs()
                    ? "java.exe"
                    : "java"
            def javaExe = new File(binDir, javaExeName)

            if (javaExe.exists()) {
                def jdkHome = binDir.parentFile
                if (jdkHome != null) {
                    def candidateTools = new File(jdkHome, "lib/tools.jar")
                    if (candidateTools.exists()) {
                        toolsFromPath = candidateTools.canonicalFile
                    }
                }
            }
        }

        if (toolsFromPath != null) {
            return toolsFromPath
        }
    }

    throw new GradleException(
            "tools.jar not found. Install a Java 8 JDK and ensure it is reachable via java.home, JAVA_HOME or PATH."
    )
}

// Detect Windows OS in a minimal way
def isWindowsOs() {
    def name = System.properties['os.name']
    return name != null && name.toLowerCase().contains("win")
}

// tools.jar für JavaCompiler (nur Java 8, ab Java 9 JShell verwenden!)
def javaToolsJar = resolveToolsJar()

dependencies {
    implementation project(':playwright-adapter')
    implementation project(':playwright-java')

    implementation project(':wd4j')

    // Debug
    implementation "com.appland:appmap-agent:1.28.0"
    implementation 'ch.qos.logback:logback-classic:1.2.11' // letzte Java 8-kompatible Version

    //Core
    implementation 'org.java-websocket:Java-WebSocket:1.5.2'
    implementation 'com.google.code.gson:gson:2.8.9'

    // Für JSON-Mapper: Finden der Wrapper-Implementierer (kann auch mit einer Klassenliste ersetzt werden)
    implementation 'org.reflections:reflections:0.10.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:3.12.4'

    implementation 'org.slf4j:slf4j-api:1.7.36'  // SLF4J API für Java 8
    implementation 'ch.qos.logback:logback-classic:1.2.11'  // Logback als Implementierung

    // Windows Key Store
    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'net.java.dev.jna:jna-platform:5.13.0'

    // otp auth
    implementation 'com.warrenstrange:googleauth:1.4.0'

    // pdf Erzeugung
    implementation 'org.apache.pdfbox:pdfbox:2.0.27' // Letzte Version mit Java 8-Support

    // Pretty Print
    implementation 'com.google.googlejavaformat:google-java-format:1.7'

    // tools.jar für JavaCompiler (nur Java 8, ab Java 9 JShell verwenden!)
    implementation files(javaToolsJar)

    // Code-Editor
    implementation 'com.fifesoft:rsyntaxtextarea:3.3.3'
    implementation 'com.fifesoft:autocomplete:3.3.1'   // Add-on für Code-Completion
    // optional: vordefinierte Language-Supports (Java, JS, …)
    // implementation 'com.fifesoft:languagesupport:3.3.3'

    // JavaScript
    implementation 'org.graalvm.js:js:22.3.0'

    // JSON
    implementation 'com.google.code.gson:gson:2.10.1'

    // JavaCV API nur zur Kompilierung, Runtime optional via Loader
    compileOnly "org.bytedeco:javacv:1.5.10"
    
    // vlcj für LibVLC-basierte Videoaufzeichnung (optional, compile-only)
    // Wenn auf dem Classpath, wird LibVLC bevorzugt, sonst Fallback auf FFmpeg/JavaCV
    // Verwendung: Füge zur Runtime hinzu, wenn VLC lokal installiert ist
    // compileOnly 'uk.co.caprica:vlcj:4.8.2'
}


jar {
    manifest {
        attributes(
                'Main-Class': 'de.bund.zrb.Main' // Die Main-Klasse, die gestartet werden soll
        )
    }
}

shadowJar {
    // Schließe sicherheitshalber platform/natives aus, falls transitive reinrutschen
    dependencies {
        exclude(dependency('org.bytedeco:.*-platform:.*'))
        exclude(dependency('org.bytedeco:.*:.*:windows.*'))
        exclude(dependency('org.bytedeco:.*:.*:linux.*'))
        exclude(dependency('org.bytedeco:.*:.*:macosx.*'))
    }
}
