plugins {
    id 'java'
    id 'application'
}

group = 'de.bund.zrb'
version = '2.0.0'

sourceCompatibility = 1.8

application {
    // Gradle 8+ API
    mainClass = 'de.bund.zrb.Main'
}

repositories { mavenCentral() }

dependencies {
    implementation project(':wd4j')
    implementation project(':playwright-adapter')
    implementation project(':playwright-java')

    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'ch.qos.logback:logback-classic:1.2.11'

    implementation 'org.java-websocket:Java-WebSocket:1.5.2'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.reflections:reflections:0.10.2'

    implementation 'org.apache.pdfbox:pdfbox:2.0.27'

    implementation 'com.fifesoft:rsyntaxtextarea:3.3.3'
    implementation 'com.fifesoft:autocomplete:3.3.1'

    implementation 'org.graalvm.js:js:22.3.0'

    implementation 'com.warrenstrange:googleauth:1.4.0'

    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'net.java.dev.jna:jna-platform:5.13.0'

    implementation 'com.google.googlejavaformat:google-java-format:1.7'

    // Video libs werden zur Laufzeit geladen (kein compile/runtime Eintrag hier)
}

task copyRuntimeLibs(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/dist/libs"
}

jar {
    doFirst {
        def coreJars = configurations.runtimeClasspath.files.collect { 'libs/' + it.name }
        manifest {
            attributes(
                'Main-Class': application.mainClass.get(),
                'Class-Path': coreJars.join(' ')
            )
        }
    }
}

// Einheitliche Distribution: build/dist/app.jar + build/dist/libs/
// Verwende eindeutigen Tasknamen, um Kollisionen zu vermeiden
tasks.register('dist', Copy) {
    dependsOn jar, copyRuntimeLibs
    from("$buildDir/libs/${project.name}-${version}.jar") { rename { 'app.jar' } }
    from("$buildDir/dist/libs") { into 'libs' }
    into "$buildDir/dist"
}

build.dependsOn 'dist'
